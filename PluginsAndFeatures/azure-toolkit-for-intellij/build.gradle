plugins {
    id "org.jetbrains.intellij" version "0.3.4"
    id "org.jetbrains.kotlin.jvm" version "1.2.50"
}

import org.apache.tools.ant.filters.*
processResources {
    filesMatching('**/ApplicationInsights.xml') {
        filter(ReplaceTokens, tokens: ["applicationinsights.key": project.property("applicationinsights.key")])
    }
}

group 'com.microsoft.azuretools'

compileJava {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

apply plugin: 'checkstyle'
apply plugin: 'findbugs'

checkstyle {
    toolVersion = '7.7'
    configFile = new File('config/checkstyle/checkstyle.xml')
    showViolations = false
}

findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = true
}

intellij {
    pluginName = 'azure-toolkit-for-intellij'
    version = intellij_version
    updateSinceUntilBuild = Boolean.valueOf(updateVersionRange)
    plugins = ['maven', dep_plugins]
	downloadSources = Boolean.valueOf(sources)
}

sourceSets {
    main {
        java.srcDirs 'src'
        kotlin.srcDirs 'src'
        resources.srcDirs 'resources'
    }
    test {
        java {
            srcDir 'Test/java'
        }
        resources {
            srcDir 'Test/resources'
        }
    }
}

repositories {
    maven {url uri('../../.repository')} // to snap to the private maven repo on Jenkins if any
    mavenLocal()
    mavenCentral()
}

configurations {
    compile.exclude module:'slf4j-api'
    testCompile.exclude module:'htmlunit'
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

apply plugin: 'java'

dependencies {
    compile fileTree(dir: '../AddLibrary/AzureLibraries/com.microsoft.azuretools.sdk/dependencies', include: ['applicationinsights-management-1.0.3.jar'])
    compile 'com.microsoft.sqlserver:mssql-jdbc:6.1.0.jre8'
    compile 'commons-io:commons-io:2.5'
    compile 'com.microsoft.azure:azure-client-runtime:1.5.0', { force = true }
    compile 'com.microsoft.azure:azure-client-authentication:1.5.0', { force = true }
    compile 'com.microsoft.azuretools:azuretools-core:3.8.0', {
        exclude group: "com.microsoft.azure", module: "azure-client-authentication"
        exclude group: "com.microsoft.azure", module: "azure-client-runtime"
    }
    compile 'com.microsoft.azuretools:azure-explorer-common:3.8.0', {
        exclude group: "com.microsoft.azure", module: "azure-client-authentication"
        exclude group: "com.microsoft.azure", module: "azure-client-runtime"
    }
    compile 'com.microsoft.azuretools:spark-tools:0.1.0'
    compile 'com.microsoft.azuretools:hdinsight-node-common:3.8.0', {
        exclude group: "com.microsoft.azure", module: "azure-client-authentication"
        exclude group: "com.microsoft.azure", module: "azure-client-runtime"
    }
    compile 'com.spotify:docker-client:8.6.2'

    testCompile 'junit:junit:4.12'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile 'info.cukes:cucumber-java:1.2.5'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile 'org.assertj:assertj-swing-junit:3.5.0'

    testCompile 'com.github.tomakehurst:wiremock:2.8.0'
    testCompile 'org.powermock:powermock-module-junit4:1.7.0RC4'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.0RC4'
    testCompile 'javax.servlet:javax.servlet-api:3.1.0'
}

test {
    testLogging.showStandardStreams = true
    testLogging {
        events "passed", "skipped", "failed"
    }
} 

task cucumberPackJar(type: Jar) {
    appendix = 'pathing'

    doFirst {
        manifest {
            attributes "Class-Path": configurations.cucumberRuntime.files.collect {
                it.toURL().toString().replaceFirst(/file:\/+/, '/')
            }.join(' ')
        }
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava, cucumberPackJar
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = files(sourceSets.main.output, sourceSets.test.output, cucumberPackJar.archivePath)
            args = [
                '--plugin', 'pretty',
                '--glue', 'com.microsoft.azure.hdinsight.spark.common',
                'Test/resources']
        }
    }
}

test.dependsOn cucumber

defaultTasks 'buildPlugin', 'test'

task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
